apply plugin: 'groovy'
apply plugin: 'scala'
apply plugin: 'idea'

buildscript {
    ext {
        springVersion = '1.1.6.RELEASE'
        h2Version = '1.4.181'
    }
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springVersion"
        classpath "com.h2database:h2:$h2Version"
        // gradle plugin
        classpath "org.flywaydb:flyway-gradle-plugin:3.0"
        classpath 'com.wiredforcode:gradle-heroku-plugin:1.0.0'
    }
}

apply plugin: 'spring-boot'

bootRun {
    jvmArgs "-Dspring.profiles.active=dev"
}

ext {
    scalaVersion = '2.11'
    scalaFullVersion = "${scalaVersion}.2"
    scalikejdbcVersion = '2.1.1'
}

repositories {
    jcenter()
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-web:$springVersion"

    compile "com.h2database:h2:$h2Version"
    runtime 'org.bgee.log4jdbc-log4j2:log4jdbc-log4j2-jdbc4.1:1.16'

    // scalikejdbc
    compile "org.scala-lang:scala-library:$scalaFullVersion"

    compile "org.scalikejdbc:scalikejdbc_$scalaVersion:$scalikejdbcVersion"
    compile "org.scalikejdbc:scalikejdbc-config_$scalaVersion:$scalikejdbcVersion"
    runtime 'ch.qos.logback:logback-classic:1.1.2'
    testCompile "org.scalikejdbc:scalikejdbc-test_$scalaVersion:$scalikejdbcVersion"
    testCompile 'org.scalatest:scalatest_2.11:2.2.2'
    testRuntime 'org.scala-lang.modules:scala-xml_2.11:1.0.2'

    // java
    compile 'com.google.guava:guava:18.0'
    compile 'commons-dbutils:commons-dbutils:1.6'
    testCompile 'junit:junit:4.11'

    // groovy
    compile 'org.codehaus.groovy:groovy-all:2.3.6'
    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'

}

wrapper {
    gradleVersion '2.1'
}

sourceCompatibility = targetCompatibility = 1.7

tasks.withType(JavaCompile) {
//    options.incremental = true
    options.encoding = 'UTF-8'
}

tasks.withType(ScalaCompile) {
    scalaCompileOptions.useAnt = false
}

test {
    exclude '**/*$*'
}

idea {
    module {
        jdkName = '1.7'
    }
}

apply plugin: 'flyway'

flyway {
    user = 'sa'
    password = ''
    driver = 'org.h2.Driver'
    url = 'jdbc:h2:file:./db/default'
    initVersion = 0
}

apply plugin: 'heroku'

heroku {
    apiKey = project.hasProperty("herokuApiKey") ? "$herokuApiKey": ""
    appName = 'serene-scrubland-5745'
}

defaultTasks "clean", "assemble"
